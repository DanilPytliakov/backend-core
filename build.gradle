plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'gg.jte.gradle' version '3.1.15'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'ru.mentee.power'
version = '1.0-SNAPSHOT'

ext {
    junitVersion = '5.10.0'
    assertjVersion = '3.25.3'
    servletVersion = '6.1.0'
    tomcatVersion = '11.0.1'
    jteVersion = '3.1.15'
    springBootVersion = '3.2.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    mainClass = 'ru.mentee.power.crm.Main'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.21.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.21.0'
    implementation "jakarta.servlet:jakarta.servlet-api:${servletVersion}"
    implementation "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    implementation "gg.jte:jte:${jteVersion}"
    implementation "gg.jte:jte-runtime:${jteVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
}

checkstyle {
    toolVersion = '13.0.0'  // актуальная версия на 2025 год для Gradle 9.1
    maxErrors = 0      // даже 1 ошибка (severity=error) → BUILD FAILED
    maxWarnings = 999  // предупреждения (severity=warning) не блокируют сборку
    // configFile = file("${rootDir}/config/checkstyle/checkstyle.xml") // опционально, custom config
}

// Настройка JaCoCo
jacoco {
    toolVersion = '0.8.14'  // актуальная версия на 2025 год с поддержкой Java 25
}

// Настройка тестов
test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport  // генерировать отчёт после тестов
}

// Настройка JaCoCo отчёта
jacocoTestReport {
    dependsOn test  // отчёт генерируется после выполнения тестов
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }
}

// Настройка проверки покрытия (минимум 80%)
jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.70  // 70% покрытия инструкций
            }
        }
    }
}

// Интеграция проверок в lifecycle
check {
    dependsOn jacocoTestCoverageVerification
}

test {
    useJUnitPlatform()
}

jte {
    sourceDirectory = layout.projectDirectory.dir("src/main/jte").asFile.toPath()
    contentType = "Html"
    binaryStaticContent = false
}

task bootRun8081(type: org.springframework.boot.gradle.tasks.run.BootRun) {
    group = 'application'
    description = 'Run Spring Boot on port 8081'

    mainClass = 'ru.mentee.power.crm.spring.Application'
    classpath = sourceSets.main.runtimeClasspath

    jvmArgs = [
            '--enable-preview',
            '-Dserver.port=8081'
    ]

    args = ['--server.port=8081']
}